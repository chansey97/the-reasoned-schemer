#lang racket
(require "../libs/trs2/trs2-impl.rkt")
(require "../04-list/append/appendo_2_correct.rkt")
(require "../07-arithmetic/poso.rkt")
(require "../07-arithmetic/gt1o.rkt")
(require "../07-arithmetic/pluso.rkt")
(require "./mulo.rkt")
(require "./divo_3_correct.rkt")
(require "./length-comparison/eqlo.rkt")
(require "./length-comparison/ltlo.rkt")
(require "./length-comparison/ltelo.rkt")
(require "./comparison/lto.rkt")
(require "./comparison/lteo.rkt")
(require "./splito.rkt")
(provide (all-defined-out))

;; 8.84
(defrel (logo n b q r)
  (conde
    ((== '() q) (<=o n b)
     (pluso r '(1) n))
    ((== '(1) q) (>1o b) (=lo n b)
     (pluso r b n))
    ((== '(1) b) (poso q)
     (pluso r '(1) n))
    ((== '() b) (poso q) (== r n))
    ((== '(0 1) b)
     (fresh (a ad dd)
       (poso dd)
       (== `(,a ,ad . ,dd) n)
       (exp2o n '() q)
       (fresh (s)
         (splito n dd r s))))
    ((<=o '(1 1) b) (<lo b n)
     (base-three-or-moreo n b q r))))

;; 8.84
(defrel (exp2o n b q)
  (conde
    ((== '(1) n) (== '() q))
    ((>1o n) (== '(1) q)
     (fresh (s)
       (splito n b s '(1))))
    ((fresh (q1 b2)
       (== `(0 . ,q1) q) (poso q1)
       (<lo b n)
       (appendo b `(1 . ,b) b2)
       (exp2o n b2 q1)))
    ((fresh (q1 nh b2 s)
       (== `(1 . ,q1) q) (poso q1)
       (poso nh)
       (splito n b s nh)
       (appendo b `(1 . ,b) b2)
       (exp2o nh b2 q1)))))

;; 8.84
(defrel (base-three-or-moreo n b q r)
  (fresh (bw1 bw nw nw1 ql1 ql s)
    (exp2o b '() bw1)
    (pluso bw1 '(1) bw)
    (<lo q n)
    (fresh (q1 bwq1)
      (pluso q '(1) q1)
      (*o bw q1 bwq1)
      (<o nw1 bwq1))
    (exp2o n '() nw1)
    (pluso nw1 '(1) nw)
    (/o nw bw ql1 s)
    (pluso ql '(1) ql1)
    (<=lo ql q)
    (fresh (bql qh s qdh qd)
      (repeated-mulo b ql bql)
      (/o nw bw1 qh s)
      (pluso ql qdh qh)
      (pluso ql qd q)
      (<=o qd qdh)
      (fresh (bqd bq1 bq)
        (repeated-mulo b qd bqd)
        (*o bql bqd bq)
        (*o b bq bq1)
        (pluso bq r n)
        (<o n bq1)))))

;; 8.84
(defrel (repeated-mulo n q nq)
  (conde
    ((poso n) (== '() q) (== '(1) nq))
    ((== '(1) q) (== n nq))
    ((>1o q)
     (fresh (q1 nq1)
       (pluso q1 '(1) q)
       (repeated-mulo n q1 nq1)
       (*o nq1 n nq)))))
